import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    id 'java'
    id 'com.modrinth.minotaur' version '2.+'
}

defaultTasks 'build'

def buildNum = System.getenv('GITHUB_RUN_NUMBER') ?: 'SNAPSHOT'
project.group = 'net.pl3x.map.warps'
project.version = "$minecraftVersion-$buildNum"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.essentialsx.net/releases/' }
    maven { url = 'https://repo.olziedev.com/' }
    exclusiveContent {
        forRepository { maven { url = 'https://api.modrinth.com/maven/' } }
        filter { includeGroup 'maven.modrinth' }
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:$bukkitVersion") {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    compileOnly "maven.modrinth:pl3xmap:$pl3xmapVersion"

    //noinspection VulnerableLibrariesLocal
    compileOnly "net.essentialsx:EssentialsX:$essentialsVersion"
    compileOnly "com.olziedev:playerwarps-api:$playerWarpsVersion"
}

compileJava {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

javadoc {
    options.encoding = 'UTF-8'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(
                'name': rootProject.name,
                'group': project.group,
                'version': project.version,
                'authors': authors,
                'description': project.properties['description'],
                'website': website
        )
    }
}

var file = rootProject.layout.buildDirectory.file("libs/${rootProject.name}-${project.version}.jar")

modrinth {
    autoAddDependsOn = false
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'pl3xmap-warps'
    versionName = "${project.version}"
    versionNumber = "${project.version}"
    versionType = 'release'
    uploadFile = file.get()
    gameVersions.addAll(["${minecraftVersion}"])
    loaders.addAll(['bukkit', 'paper', 'purpur', 'spigot'])
    changelog = System.getenv('COMMIT_MESSAGE')
    dependencies {
        required.project "pl3xmap"
    }
}
